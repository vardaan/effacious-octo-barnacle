apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def date = new Date()
def buildTime = date.format("dd.MM.yy", TimeZone.getTimeZone("UTC"))
def buildTimeInternal = date.format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
android {
    compileSdkVersion 21
       buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.y29.appexperiments"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }
    productFlavors {
        internal {
            applicationId 'com.y29.experiments.internal'
        }
        production {
            applicationId 'com.y29.experiments'
        }
    }
}

// change apk name
android.applicationVariants.all { variant ->
    for (output in variant.outputs) {
        def outputFile = output.outputFile
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
            def fileName = "app-experiments-${output.name}-${buildTime}.apk"
            output.outputFile = new File(outputFile.parent, fileName)
        }
    }
}

// TODO remove eventually: http://b.android.com/162285
configurations {
    internalDebugCompile
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:22.2.1'
    internalDebugCompile 'me.drakeet.library:crashwoodpecker:0.9.7'
    internalDebugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    internalDebugCompile 'com.facebook.stetho:stetho:1.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'

    //remote connections
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'

    // for doing cool stuff
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'

    //for image loading
    compile 'com.squareup.picasso:picasso:2.5.2'

    //for debugging
    compile 'com.jakewharton.timber:timber:2.5.0'

    //for dependency injections
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

}
